apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "io.meshtech.koasta"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 38
        versionName "1.0"

        testInstrumentationRunner "io.meshtech.koasta.extras.TestRunner"

        testOptions {
            animationsDisabled = true
            unitTests.returnDefaultValues = true
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            manifestPlaceholders = [ squareApplicationId: "CHANGEME" ]
        }
        staging {
            dimension "environment"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            manifestPlaceholders = [ squareApplicationId: "CHANGEME" ]
        }
        live {
            dimension "environment"
            manifestPlaceholders = [ squareApplicationId: "CHANGEME" ]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        println("Iterating variant: " + variant.getName())
        if (variant.getName() == "devDebug" || variant.getName() == "devRelease") {
            def apiUrl = System.getenv("LOCAL_API_URL")?:"http://10.0.2.2:5000"
            if (apiUrl == null) {
                Properties properties = new Properties()
                def file = project.rootProject.file('env.properties')
                if (file.exists()) {
                    properties.load(file.newDataInputStream())
                    apiUrl = properties.getProperty('localApiUrl')?:"http://10.0.2.2:5000"
                }
            }
            buildConfigField "String", "API_URL", "\"$apiUrl\""
            buildConfigField "Boolean", "PAYMENTS_SANDBOX", "true"
            buildConfigField "String", "GOOGLE_API_KEY", "CHANGEME"
        } else if (variant.getName() == "stagingDebug" || variant.getName() == "stagingRelease") {
            buildConfigField "String", "API_URL", "\"https://test.api.koasta.com\""
            buildConfigField "Boolean", "PAYMENTS_SANDBOX", "true"
            buildConfigField "String", "GOOGLE_API_KEY", "CHANGEME"
        } else if (variant.getName() == "liveDebug" || variant.getName() == "liveRelease") {
            buildConfigField "String", "API_URL", "\"https://api.koasta.com\""
            buildConfigField "Boolean", "PAYMENTS_SANDBOX", "false"
            buildConfigField "String", "GOOGLE_API_KEY", "CHANGEME"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'net.swiftzer.semver:semver:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-core-ext:$koin_version"
    implementation 'com.squareup.moshi:moshi:1.8.0' // Must match the version used by Square in in-app-payments:*
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0' // Must match the version used by Square in in-app-payments:*
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.gms:play-services-auth:18.1.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0' // Must match the version used by Square in in-app-payments:*
    implementation 'com.squareup.moshi:moshi-adapters:1.8.0' // Must match the version used by Square in in-app-payments:*
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation 'com.github.kittinunf.fuel:fuel:2.2.3'
    implementation 'com.github.kittinunf.fuel:fuel-coroutines:2.2.3'
    implementation 'com.github.kittinunf.fuel:fuel-moshi:2.2.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation "org.koin:koin-test:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation 'com.github.kittinunf.fuse:fuse:1.0.0-beta2'
    implementation 'com.github.kittinunf.fuse:fuse-android:1.0.0-beta2'
    implementation "io.coil-kt:coil:0.11.0"
    implementation 'com.google.firebase:firebase-messaging:20.2.4'
    implementation 'com.google.android.gms:play-services-wallet:18.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'
    implementation 'com.atlassian.commonmark:commonmark:0.15.0'
    implementation 'com.facebook.android:facebook-core:7.1.0'
    implementation 'com.facebook.android:facebook-login:7.1.0'
    implementation ("com.squareup.sdk.in-app-payments:card-entry:1.4.0")
    implementation("com.squareup.sdk.in-app-payments:buyer-verification:1.4.0")
    implementation 'com.squareup.sdk.in-app-payments:google-pay:1.4.0'
    implementation 'com.darwinsys:hirondelle-date4j:1.5.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}
